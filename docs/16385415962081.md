iOS常见问题总结

# 1.AutoReleasePool 

## 1.0 核心

### 数据结构
```
class AutoreleasePoolPage {
    magic_t const magic;
    id *next;
    pthread_t const thread;
    AutoreleasePoolPage * const parent;
    AutoreleasePoolPage *child;
    uint32_t const depth;
    uint32_t hiwat;
};
```
![截屏2025-02-11 08.25.00](media/16385415962081/%E6%88%AA%E5%B1%8F2025-02-11%2008.25.00.png)

### objc_autoreleasePoolPush
创建自动释放池，并返回哨兵对象的地址

```
void *objc_autoreleasePoolPush(void) {
    return AutoreleasePoolPage::push();
}

 static inline void *push() 
    {
        id *dest;
        if (DebugPoolAllocation) {
            // Each autorelease pool starts on a new pool page.
            dest = autoreleaseNewPage(POOL_BOUNDARY);
        } else {
        //插入哨兵对象 并返回它的地址
            dest = autoreleaseFast(POOL_BOUNDARY);
        }
        assert(dest == EMPTY_POOL_PLACEHOLDER || *dest == POOL_BOUNDARY);
        return dest;
    }
    
    static inline id *autoreleaseFast(id obj)
    {
        AutoreleasePoolPage *page = hotPage();
        if (page && !page->full()) {
        //当前page未满 直接插入
            return page->add(obj);
        } else if (page) {
        //已满 新建page
            return autoreleaseFullPage(obj, page);
        } else {
        //无page得情况，即第一个自动释放池
            return autoreleaseNoPage(obj);
        }
    }
    
    id *add(id obj)
    {
        assert(!full());
        unprotect();
        //ret指向next当前位置
        id *ret = next;  // faster than `return next-1` because of aliasing
        *next++ = obj; //obj赋值给next当前指向位置, next指针递增 指向下一个位置
        protect();
        return ret;
    }
```

### objc_autoreleasePoolPop
对当前page栈顶到传入的哨兵对象，这一段的内容 发送release 消息

![截屏2025-02-11 08.30.07](media/16385415962081/%E6%88%AA%E5%B1%8F2025-02-11%2008.30.07.png)


```
void objc_autoreleasePoolPop(void *ctxt) {
    AutoreleasePoolPage::pop(ctxt);
}

//简化过后的方法
static inline void pop(void *token) {
    //获取当前 token 所在的 AutoreleasePoolPage
    AutoreleasePoolPage *page = pageForPointer(token);
    id *stop = (id *)token;
    
    //发送release消息
    page->releaseUntil(stop);
    
    //释放不需要的内存
    if (page->child) {
        if (page->lessThanHalfFull()) {
            page->child->kill();
        } else if (page->child->child) {
            page->child->child->kill();
        }
    }
}

void releaseUntil(id *stop) {
    while (this->next != stop) {
        AutoreleasePoolPage *page = hotPage();

        while (page->empty()) {
            page = page->parent;
            setHotPage(page);
        }

        page->unprotect();
        id obj = *--page->next;
        memset((void*)page->next, SCRIBBLE, sizeof(*page->next));
        page->protect();

        if (obj != POOL_SENTINEL) {
            objc_release(obj);
        }
    }

    setHotPage(this);
}
```

### autorelease
```
- [NSObject autorelease]
└── id objc_object::rootAutorelease()
    └── id objc_object::rootAutorelease2()
        └── static id AutoreleasePoolPage::autorelease(id obj)
            └── static id AutoreleasePoolPage::autoreleaseFast(id obj)
```
最终都会调用到autoreleaseFast(内部分3种情况，如果无page则自动创建)方法，将obj 加入到自动释放池中

## 1.1AutoReleasePool简介
- 数据结构上：以栈为节点的双向链表。栈的节点是一个AutoReleasePage对象，它是一个4K大小的栈结构，除了56字节的成员变量，其余的用来存放autorelease对象；它有一个Parent和Child指针分别指向相邻的两页
- Push操作：调用`objc_autoreleasePoolPush`会向当前Page插入一个哨兵对象(也就是nil)，并返回它的地址
- Pop操作：调用`objc_autoreleasePoolPop`会从当前Page栈顶元素开始，依次向内部元素发送`release`消息，直到传入的哨兵对象为止。因为自动释放池是可以嵌套的，所以释放操作可能跨越不同的Page
- autorelase: 将对象放入自动释放池(如果没有则创建Page),与是否在主线程，是否有runloop都无关。是否有runloop，只关乎释放的时机

## 1.2AutoReleasePool的释放时机
- 手动创建：`@autoreleasepool{}`作用域结束的时候释放
- 系统创建：应用启动后，runloop注册了两个Observer:第一个监测的事件是，即将进入runloop(kCFRunLoopEntry)，事件触发时调用`objc_autoreleasePoolPush`创建自动释放池，它的优先级最高，确保在进入runloop的时候，自动释放时已经创建完毕；另外一个监测的事件是，即将进入休眠或退出(kCFRunLoopBeforeWaiting,kCFRunLoopExit)此时会调用`objc_autoreleasePoolPop`和`objc_autoreleasePoolPush`来**释放旧池创建新池**，它的优先级最低，确保在其他操作之后。
- 其他：通过实测，在处理完Timer和Source事件之后，也插入了释放操作
- runtime中load方法的调用也插入了自动释放池
 ```
 void call_load_methods(void)
{
    static bool loading = NO;
    bool more_categories;

    loadMethodLock.assertLocked();

    // Re-entrant calls do nothing; the outermost call will finish the job.
    if (loading) return;
    loading = YES;

    void *pool = objc_autoreleasePoolPush();

    do {
        // 1. Repeatedly call class +loads until there aren't any more
        while (loadable_classes_used > 0) {
            call_class_loads();
        }

        // 2. Call category +loads ONCE
        more_categories = call_category_loads();

        // 3. Run more +loads if there are classes OR more untried categories
    } while (loadable_classes_used > 0  ||  more_categories);

    objc_autoreleasePoolPop(pool);

    loading = NO;
}
 ````
 - gcd中workthread 的方法调用，在while循环中不断取任务去执行。
  ```
  #if DISPATCH_COCOA_COMPAT
	// ensure that high-level memory management techniques do not leak/crash
	dispatch_begin_thread_4GC();
	void *pool = _dispatch_begin_NSAutoReleasePool();
#endif

#if DISPATCH_PERF_MON
	uint64_t start = mach_absolute_time();
#endif
	while ((item = fastpath(_dispatch_queue_concurrent_drain_one(dq)))) {
		_dispatch_continuation_pop(item);
	}
#if DISPATCH_PERF_MON
	_dispatch_queue_merge_stats(start);
#endif

#if DISPATCH_COCOA_COMPAT
	_dispatch_end_NSAutoReleasePool(pool);
	dispatch_end_thread_4GC();
#endif
  ```
 
## 1.3 ARC对于AutoReleasePool中内存管理的优化
```
+ (instancetype)createSark {
    return [self new]; //自动释放的对象
}
// caller
Sark *sark = [Sark createSark];
```
>在返回值身上调用`objc_autoreleaseReturnValue`方法时，runtime将这个返回值object储存在TLS中，然后直接返回这个object（不调用autorelease）；同时，在外部接收这个返回值的`objc_retainAutoreleasedReturnValue`里，发现TLS中正好存了这个对象，那么直接返回这个object（不调用retain）。

于是乎，调用方和被调方利用TLS做中转，很有默契的免去了对返回值的内存管理。
## 1.4 子线程中的AutoReleasePool的释放时机
- 子线程无runloop时： 在线程调用start之后，虽然thread对象还在，但是线程已经exit了，已无法在该线程执行任务了，此时会调用tls_dealloc对线程占用的资源做一次清空操作，其中就包括自动释放池的清空。**不过虽然线程释放的时候，会清空自动释放池，但是如果如果你不主动在子线程创建pool的话，会造成子线程中autorelease对象延迟释放，占用内存过多，所以最好还在主动创建**
- 子线程中有runloop时：timer/source事件之后会插入清空操作。但与主线程不同的时，子线程runloop没有注册kCFRunLoopEntry/kCFRunLoopExit/kCFRunLoopBeforeWaiting 这几个事件的观察者。(子线程runloop默认会带有source，也就是说子线程只要有runloop就可以工作，不需要向其中手动加入Source和timer。)

[带着问题看源码----子线程AutoRelease对象何时释放](https://suhou.github.io/2018/01/21/%25E5%25B8%25A6%25E7%259D%2580%25E9%2597%25AE%25E9%25A2%2598%25E7%259C%258B%25E6%25BA%2590%25E7%25A0%2581----%25E5%25AD%2590%25E7%25BA%25BF%25E7%25A8%258BAutoRelease%25E5%25AF%25B9%25E8%25B1%25A1%25E4%25BD%2595%25E6%2597%25B6%25E9%2587%258A%25E6%2594%25BE/)

## 1.5 自动释放池的使用场景
据Apple文档，Cocoa总是希望代码在自动释放池中执行。
为什么呢？AutoRelease的对象如果外部没有对象强引用它，那么就是应该释放，但是如果没有自动释放池那么它的释放时机将会被推迟，可能出现内存高峰；同时即便被外部强引用，那么也应该调用一次release抵消掉创建是的引用计数。

另外Apple推荐，在以下三种场景下使用自动释放池
- 如果你编写的程序不是基于 UIKit 框架的，比如说图片调整大小、文件读取(可参考SDWebImage)；
- 如果你编写的循环中创建了大量的临时对象；
- 开启子线程，且子线程中有可能累计自动释放对象时(如RN、作用类似于GCD中的autoreleasepool，在执行任务时，插入自动释放池，因为你也不要调用者都要干些什么，插入一个池总是好的)。
- 自己加一条：长时间在后端运行的任务，runloop失效了。
```
void RCTMessageThread::runAsync(std::function<void()> func)
{
  CFRunLoopPerformBlock(m_cfRunLoop, kCFRunLoopCommonModes, ^{
    // Create an autorelease pool each run loop to prevent memory footprint from growing too large, which can lead to
    // performance problems.
    @autoreleasepool {
      func();
    }
  });
  CFRunLoopWakeUp(m_cfRunLoop);
}
```

总之，多插入自动释放池是没错的。

## 1.6 那么什么样的对象时AutoRelease的？
- 通过类方法创建的对象
- 除此之外呢？

## 1.7 自动释放池与runloop
runloop的状态，以及处理timer和source的时候，确实提供了很多的时机去释放autorelease对象，
但除此之外，还有load方法、gcd中调用任务时候、usingBlock遍历数组、子线程exit等时机。
runloop只是提供了一个很好的时机处理autorelease对象，并不是说自动释放池依赖runloop。

可结合1.5一起看

## 1.8 题目
### 题目1
```
/**
* 下面的对象 ，分别在什么地方被释放 ?
*/
- (void)weakLifeCycleTest {
   id obj0 = @"iTeaTime(技术清谈)";
   __weak id obj1 = obj0;
   id obj2 = [NSObject new];
   __weak id obj3 = [NSObject new];
   {
       id obj4 = [NSObject new];
   }
   __autoreleasing id obj5 = [NSObject new];
   __unsafe_unretained id obj6 = self;
   NSLog(@"obj0=%@, obj1=%@, obj2=%@, obj3=%@, obj5=%@, obj6=%@", obj0, obj1, obj2, obj3, obj5, obj6);
   // Lots of code ...
}
```
**obj0 字符串属于常量区，不会释放**
**obj1 指向的对象在常量区，不会释放**
obj2 没有修复符，默认为 __strong ，会在对象被使用结束时释放。如果下方没有使用该对象，根据编译器是否优化，可能在下一行直接销毁，最晚可以在方法结束时销毁。
obj3 警告 “Assigning retained object to weak variable; object will be released after assignment” ，new 结束后，等号右侧对象立马被释放，左侧指针也立即销毁，下方打印也是 null
obj4 出了最近的括号销毁
obj5 出了最近的一个 autoreleasePool 时被释放
obj6 类似于基本数据结构的修饰符号 assign ，不会对修饰对象的生命周期产生影响，随着self的释放，obj6也会随之释放。比如 self 被其它线程释放，那么obj6也会随之释放。


# 2 Category 
## 2.1 Category 为什么无法添加实例变量
1. 编译过后对象内存布局已经确定，而Category是在程序启动之后附着在本类上的两者是不同阶段的内容, 如何添加实例变量则会破坏类的内存布局
2. Category在底层对应的是一个Category_t的结构体，这个结构体就没有保存实例变量的数据结构
```
typedef struct category_t { 
const char *name; //类的名字
classref_t cls; //编译期间没有值，运行时根据name找到其类对象
struct method_list_t *instanceMethods; //实例方法
struct method_list_t *classMethods;//类方法
struct protocol_list_t *protocols; //协议
struct property_list_t *instanceProperties; //属性
} category_t;
```
另外一种解答：
1.首先Categroy中没有对应的数据结构存储实例变量
2.Category是运行时加载的，而类中的实例变量是编译后就确定的，假如向编译后的类添加实例变量，
后导致内存布局发生变化。**程序编译是以单个文件为单位编译，生成目标文件，这时文件的.text, .data节已经确定了，在链接时经过符号解析和重定位生成可执行文件，这时在虚拟地址空间中实例变量的地址也是确定的，如果强行添加实例变量，会导致整个DATA segment中的内容发生偏移，是灾难性的**【答出这一段不就是加分项嘛】
3.实例变量读取的示例代码：
以属性设置为例：
```
static inline void reallySetProperty(id self, SEL _cmd, id newValue, ptrdiff_t offset, bool atomic, bool copy, bool mutableCopy)
{
    //以对象地址 + 属性偏移量 = 属性对应偏移量的地址
    id *slot = (id*) ((char*)self + offset);
    }
```
4.类的实例变量存储到ro中，是只读的
```
struct class_rw_t {
    // Be warned that Symbolication knows the layout of this structure.
    uint32_t flags;
    uint32_t version;
    
    //
    const class_ro_t *ro;

    method_array_t methods;
    property_array_t properties;
    protocol_array_t protocols;

    Class firstSubclass;
    Class nextSiblingClass;
}

struct class_ro_t {
    uint32_t flags;
    uint32_t instanceStart;
    uint32_t instanceSize;
#ifdef __LP64__
    uint32_t reserved;
#endif

    const uint8_t * ivarLayout;
    
    const char * name;
    method_list_t * baseMethodList;
    protocol_list_t * baseProtocols;
    const ivar_list_t * ivars;

    const uint8_t * weakIvarLayout;
    property_list_t *baseProperties;
}
```

## 2.2 Category & Extension区别，能给NSObject添加Extension吗，结果如何
1. 不能，需要有本类的代码才可以添加Extension

2. 区别
2.1 加载时机不同：**Extension 编译时，Category 运行时**(这点很重要)
2.2 Category有声明和实现，Extension是方法声明，实现在.m文件中
2.3 Extension可以添加实例变量，而Category不可以
## 2.3 编译器对于Category都做了什么操作
可以通过Clang命名重写源代码，得到编译后的内容
- 通过上面的描述知道，Category在底层对应的是Category_t这个结构，那么编译器会扫描分类的内容，生成构造Category_t所需的物料，如方法列表，属性列表
- 用上一步生成的物料，来初始化category_t结构体
- 将上一步生成的内容，**放在MachO文件DATA段下，obj_catlist section中**，用于运行时runtime的加载
## 2.4 Category如何被加载的
此**问题应该区分编译期和运行期两部分**，编译期的工作在上一点已经讲了。运行期的工作如下：

调用流程大概是 objc_init -> mapImages -> readImages 这样，核心在 readImages中
想dyld注册回调，每一个镜像加载后 都会调用 mapImage和loadImages方法，其中map用于加载类，load 用来调用其中类的load方法
```
void _objc_init(void)
{
    static bool initialized = false;
    if (initialized) return;
    initialized = true;
    
    // fixme defer initialization until an objc-using image is found?
    environ_init();
    tls_init();
    static_init();
    lock_init();
    exception_init();

    _dyld_objc_notify_register(&map_images, load_images, unmap_image);
}
```

具体加载分为两部分，实例对象的部分：实例方法、属性、协议，这里内容是从上面obj_catlist 读取出来的，被倒序添加到本类上；类对象部分：类方法、类协议。

运行期的工作：
从MachO文件中取出所有的CategoryList数组，将内容添加到本类的class_rw_t中，包括：方法、属性、协议。添加之后如果分类和本类有同名的方法、属性、协议，调用顺序是：后编译的分类 > 先编译的分类 > 本类的

## 2.5 多个category和本类的同名方法的加载顺序
1. 首先分类的方法是在本类之前的 (所以看起来像是类的方法被覆盖了)
2. 分类的方法按照编译顺序读取到 obj_catlist 中，这里面的方法是倒序添加的。
3. 最终的结果就是：后编译的分类 > 先编译的 > 本类的方法

## 2.6 load方法的加载顺序
首先load方法由系统通过函数地址找到并调用，且（系统）只调用一次，调用时机在main函数之前

其次顺序问题：
- 本类在分类之前
- 父类在子类之前
- 同一个类的多个分类，先编译的load方法先调用

## 2.7 load 方法与initialize的区别
1.调用时机不同

-   load 是类被加载的时候调用
-   initialize 是当类第一次接收到消息的时候调用

2.调用方式不同

-   load 是通过函数地址直接调用， 内部不调用的父类的load方法
-   initialize 是通过`objc_msgSend`调用， 内部会判断父类的initialize 方法有没有调用, 如果未调用 则主动调用

3.当子类和分类都实现该方法时 调用顺序不一样

-   load 方法,父类/子类/分类 的load 方法,都会调用,且父类的load方法调用在子类之前,类的load方法在分类的load 方法之前,先编译的类在后编译的类之前
-   initialize ,**父类先调用子类后调用.因为走的是消息机制所以,分类的 initialize 方法会覆盖本类的 initialize 方法;当前子类没有 initialize 时,转而会去调用父类的 initialize 方法，父类的initialize方法会被多次调用**

4. 对于父类方法的调用处理不同。
- load 在prepareLoads时，先把父类的加载到数组中，确保父类的load先执行。但是在load方法内部并不会主动调用父类的load
- initialize内部会检查父类是否被初始化，如果没有 先调用父类的initialize

```
void call_load_methods(void)
{
    static bool loading = NO;
    bool more_categories;

    loadMethodLock.assertLocked();

    // Re-entrant calls do nothing; the outermost call will finish the job.
    if (loading) return;
    loading = YES;

    void *pool = objc_autoreleasePoolPush();

    do {
        // 1. Repeatedly call class +loads until there aren't any more
        while (loadable_classes_used > 0) {
            call_class_loads();
        }

        // 2. Call category +loads ONCE
        more_categories = call_category_loads();

        // 3. Run more +loads if there are classes OR more untried categories
    } while (loadable_classes_used > 0  ||  more_categories);

    objc_autoreleasePoolPop(pool);

    loading = NO;
}
```
## 2.8 hook方法时，应该注意哪些，该如何hook
时机选择：一般在load方法，因为该方法在main函数之前，可以保证在开始业务代码之前方法已交换为期望的类型。同时应当注意：过多的load方法会对程序的启动造成影响，要减少不必要的hook

安全上：为了避免潜在的调用风险，一般对load方法内部的逻辑添加dispatch——once锁定

完整性：对于系统方法要调用原始实现，避免对系统产生影响

category 参考文章：
https://tech.meituan.com/2015/03/03/diveintocategory.html

# 3 关联对象
## 3.1 关联对象的工作原理
1. `AssociationsHashMap`管理app内所有的关联对象，key为对象地址，value为`ObjectAssociationMap`,它管理该对象所有的关联对象。
2. `ObjectAssociationMap`中以关联对象api中设置的key参数作为map的key，value即为`ObjcAssociation`，它包装了被关联对象和内存管理策略
3. 设置了关联对象的对象，在arm64架构之后，它的isa指针中是否有关联对象的标志位被置位true
## 3.2 被关联对象需要在dealloc方法中手动置位nil吗
不需要，在dealloc中会判断当前对象是否有关联对象，如果有会做清除工作：①移出哈希map②**发送 release 消息(而不是置位nil，这里是和set时内存管理策略对应的)**
## 3.3 使用时的注意
1. 设置好内存管理策略
2. 设置关联对象的property不要与本类的属性同名。因为关联对象一般在分类中执行，而分类的属性像方法一样，也会被添加到本类上，且在查找时，是优先查找分类的，所以会覆盖本来的属性。如不要在VC的Category中设置一个view属性的关联对象，会导致黑屏

## 3.4 关联对象与weak 
### 如何实现weak的内存管理
1.copy和retain都在传入对象上直接调用copy和retain方法即可，而使用weak描述 你需要有一个weak指针指向该对象。**其他的实现与原有的实现完全一直，将`acquireValue`的值放入哈希表存储即可**。实现参考方案1
2.取值的时候，判断内存管理策略，如果是weak，则从哈希表中取出YFWeakRef，在取出obj对象即可。
源码中内存管理的部分

//方案1
```
static id acquireValue(id value, uintptr_t policy) {
    switch (policy & 0xFF) {
    case OBJC_ASSOCIATION_SETTER_RETAIN:
        return objc_retain(value);
    case OBJC_ASSOCIATION_SETTER_COPY:
        return ((id(*)(id, SEL))objc_msgSend)(value, SEL_copy);
        
        /**
        如果需要添加weak，则添加一个类，包含一个weak指针
        @interface YFWeakRef: NSObject 
        @property(nonatmic, weak) id obj;
        @end
        
        用该obj指针指向value即可。
        */
    }
    return value;
}
```

//方案2: 优点就是不需要改动源码。同样需要一个对象包装一下
```
@interface WeakAssociatedObjectWrapper : NSObject

@property (nonatomic, weak) id object;

@end

@implementation WeakAssociatedObjectWrapper

@end
---------------
@interface UIView (ViewController)

@property (nonatomic, weak) UIViewController *vc;

@end

@implementation UIView (ViewController)

- (void)setVc:(UIViewController *)vc {
    WeakAssociatedObjectWrapper *wrapper = [WeakAssociatedObjectWrapper new];
    wrapper.object = vc;
    objc_setAssociatedObject(self, @selector(vc), wrapper, OBJC_ASSOCIATION_RETAIN_NONATOMIC);
}

- (UIViewController *)vc {
    WeakAssociatedObjectWrapper *wrapper = objc_getAssociatedObject(self, _cmd);
    return wrapper.object;
}

@end
```
这里会对wrapper进行retain，但是当self释放的时候，也会将wrapper 发送release消息，所以不用担心内存泄漏。

参考：https://zhangbuhuai.com/post/weak-associated-object.html

### 为什么系统不提供weak 内存管理策略呢？
weak 指向不会增加引用计数，且在weak对象释放时，会将所有指向它的指针置位nil。
也就是说设给当前对象绑定一个weak 描述的value，当这个value释放时weak指针会置位nil，也就是说**在获取关联对象时可能不能取到有效的值**

其次，用weak的作用可能是不影响指向的对象的引用计数，而关联对象的copy/retain 在对象释放的时候 会自动做release 也不会影响引用计数

## 3.5 关键字 @dynamic 和 @sythesize
@sythesize： 自动生成属性的setter 和 getter 
@dynamic：告诉编译器，不要自动生成setter和getter

## 3.6 关联对象的内存管理策略
```
typedef OBJC_ENUM(uintptr_t, objc_AssociationPolicy) {
    OBJC_ASSOCIATION_ASSIGN = 0,           /**< Specifies a weak reference to the associated object. */
    OBJC_ASSOCIATION_RETAIN_NONATOMIC = 1, /**< Specifies a strong reference to the associated object. 
                                            *   The association is not made atomically. */
    OBJC_ASSOCIATION_COPY_NONATOMIC = 3,   /**< Specifies that the associated object is copied. 
                                            *   The association is not made atomically. */
    OBJC_ASSOCIATION_RETAIN = 01401,       /**< Specifies a strong reference to the associated object.
                                            *   The association is made atomically. */
    OBJC_ASSOCIATION_COPY = 01403          /**< Specifies that the associated object is copied.
                                            *   The association is made atomically. */
};
```
即atomic的retain 和 copy，和nonatomic的retain 和 copy ，以及assign(类似于unsafe_unretain)